using JDTAST__ecore, Q1View__ecore;

rule filter(var visID:int, var counter:int):(int) {
	type:dom_TypeDeclaration;
	type -:dom_AbstractTypeDeclaration_name-> name:dom_SimpleName;
	method:dom_MethodDeclaration <-:dom_AbstractTypeDeclaration_bodyDeclarations- type;
	method -:dom_MethodDeclaration_returnType-> returnType:dom_SimpleType;
	returnType -:dom_SimpleType_name-> returnName:dom_SimpleName;
	publicModifier:dom_Modifier <-:dom_BodyDeclaration_modifiers- method;  
	staticModifier:dom_Modifier <-:dom_BodyDeclaration_modifiers- method;  
	
	if { !type.visited[visID];
		 name._identifier == returnName._identifier;
	     publicModifier._public == true;
	     staticModifier._static == true;
	}
	
	modify {
		eval {
			type.visited[visID] = true;
		}
		emit("\t<View:Class xmi:id=\"a" + counter + "\" name=\"" + name._identifier + "\"/>\n");
		
		return (counter + 1);
	}
}

rule xmlPrefix {
	modify {
		emit("<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n<xmi:XMI xmi:version=\"2.0\" xmlns:xmi=\"http://www.omg.org/XMI\" xmlns:xsi=\"http://www.w3.org/2001/XMLSchema-instance\" xmlns:View=\"org.amma.viewQ1\">\n");
	}
}

rule xmlSuffix {
	modify {
		emit("</xmi:XMI>\n");
	}
}

rule initXMLCounter : (int) {
	modify {
		return (1);
	}
}