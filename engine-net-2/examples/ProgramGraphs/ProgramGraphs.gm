// GrGen graph model for Program Graphs similar to the ones in "Adaptive Star Grammars for Graph Models"
// features shown: node classes, edge classes, abstract classes, connection assertions
//  constraining the types and multiplicities of the nodes allowed to be incident to the edges
// main node type is the Entity, distinguished into method bodies, expressions (including statements, those are distinguished based on context/incident edges), and declarations
// the declarations are distinguished furthermore into more specialized types
// main edge types are the contains edges defining a containment hierarchy, with the special form of next edges defining a chain of statements in a method body,
//  those define an abstract syntax tree backbone of our abstract semantic graph
// and the references edges that point from entity uses to their declarations, distinguished more fine grain into diverse types
//  those cross-edges extend the AST backbone to a complete graph containing all informations (in tradition compiler construction, those are stored in dictionaries)

// nodes //

abstract node class Entity;
node class MethodBody extends Entity;
node class Expression extends Entity;
abstract node class Declaration extends Entity;
node class Class extends Declaration;
abstract node class Feature extends Declaration;
node class MethodSignature extends Feature;
abstract node class Attribute extends Feature;
node class Constant extends Attribute;
node class Variabel extends Attribute;

// edges //

edge class contains /* abstract syntax tree part */
	connect Entity --> Entity[0:1];
edge class next extends contains
	connect MethodBody[0:1] --> Expression[0:1],
			Expression[0:1] --> Expression[0:1];
edge class references /* enriched by use-to-definition-edges */
	connect Entity --> Declaration;
edge class hasType extends references
	connect Feature[0:1] --> Class,
			Attribute[1] --> Class;
edge class bindsTo extends references
	connect MethodBody[0:1] --> MethodSignature;
edge class uses extends references
	connect Expression[0:1] --> Attribute;
edge class writesTo extends references
	connect Expression[0:1] --> Variabel;
edge class calls extends references
	connect Expression[0:1] --> MethodSignature;

// helper edge for nested layout //

edge class methodBodyContains
	connect MethodBody --> Entity[0:1];
edge class classContainsClass extends contains
	connect Class --> Class[0:1];
