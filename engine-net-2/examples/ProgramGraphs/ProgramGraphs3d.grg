// rule file containing rules and subpatterns, with the subpattern using itself recursively
// and, in contrast to the previous example containing independent patterns (which behave like negatives which must be available)
// the independents allow to match graph elements multiple times in several subpatterns (hom over all subpattern instances),
// which is normally prevented by the isomorphy constraint; the price is that they can't contain a rewrite part,
//  i.e. can only be used to formulate positive conditions
// without the independent, subpattern matching would simply fail for nodes that are reached multiple times during matching,
//  which is the case for declarations referenced from multiple uses (as the node gets locked after the first usage was matched)
// before was ProgramGraphs3c.grg, next is ProgramGraphs4.grg

#using "ProgramGraphs.gm"

#include "ProgramGraphs0WithoutUsing.grg"

rule CopyStatements(from:MethodBody, to:MethodBody)
{
	st:Statements(from);

	modify {
		st(to);
	}
}

pattern Statements(exOrMb:Entity) modify(to:Entity)
{
	alternative
	{
		Assignment {
			exOrMb -n:next-> e:Expression;
			independent {
				e -:writesTo-> :Variabel;
			}
			
			st:Statements(e);

			modify {
				delete(n);
				exOrMb <-:next- e;
				
				to -:next-> ne:Expression;
				st(ne);
			}
		}
		Call {
			exOrMb -n:next-> e:Expression;
			independent {
				e -:calls-> :MethodSignature;
			}
			
			st:Statements(e);

			modify {
				delete(n);
				exOrMb <-:next- e;
				
				to -:next-> ne:Expression;
				st(ne);
			}
		}
		Return {
			exOrMb -n:next-> e:Expression;
			independent {
				e -:uses-> :Attribute;
			}
			
			st:Statements(e);

			modify {
				delete(n);
				exOrMb <-:next- e;
				
				to -:next-> ne:Expression;
				st(ne);
			}
		}
		EndOfList {
			negative {
				exOrMb -:next->;
			}

			modify {
			}
		}
	}

	modify {
	}
}
